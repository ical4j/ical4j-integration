plugins {
    id 'signing'
    id 'pl.allegro.tech.build.axion-release' version '1.15.1'
    id 'biz.aQute.bnd.builder' version "$bndVersion" apply false
    id 'org.javamodularity.moduleplugin' version '1.8.10' apply false
}

scmVersion {
    tag {
        prefix = 'ical4j-integration-'
    }
    versionCreator 'versionWithBranch'
    branchVersionCreator = ['master': 'simple']
    nextVersion {
        suffix = 'pre'
        separator = '-'
    }
}

configure(subprojects) {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'groovy'
    apply plugin: 'biz.aQute.bnd.builder'
    apply plugin: 'org.javamodularity.moduleplugin'

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    sourceCompatibility = 11
    targetCompatibility = 11

    group = 'org.ical4j'
    description = '''\
iCalendar support for integration architectures like Apache Camel
'''
    version = rootProject.scmVersion.version

    ext {
        isReleaseVersion = !version.endsWith("SNAPSHOT")
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    dependencies {
        api "org.mnode.ical4j:ical4j:$ical4jVersion",
                "org.mnode.ical4j:ical4j-vcard:$ical4jVCardVersion"

        implementation "commons-io:commons-io:$commonsIoVersion"

        annotationProcessor 'org.osgi:osgi.core:8.0.0',
                'org.osgi:org.osgi.service.component.annotations:1.5.0',
                'org.osgi:org.osgi.service.metatype.annotations:1.4.1',
                'org.osgi:org.osgi.annotation:6.0.0'

        testImplementation "org.codehaus.groovy:groovy:$groovyVersion"

        testImplementation platform("org.spockframework:spock-bom:$spockVersion"),
                "org.spockframework:spock-core",
                "org.apache.logging.log4j:log4j-core:$log4jVersion",
                "org.apache.logging.log4j:log4j-slf4j2-impl:$log4jVersion"

        // testcontainers
        testImplementation "org.testcontainers:testcontainers:$testcontainersVersion",
                "org.testcontainers:spock:$testcontainersVersion"

    }

    jar {
        manifest {
            attributes (
                    'Implementation-Title': 'iCal4j Integration',
                    'Implementation-Version': version,
                    'Implementation-Vendor': 'Ben Fortuna'
            )
        }
    }

    compileTestJava {
        moduleOptions {
            compileOnClasspath = true
        }
    }

    test {
        moduleOptions {
            runOnClasspath = true
        }

        useJUnitPlatform()
    }

    javadoc {
        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
        options {
            links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        }
    }

    publishing {
        publications {
            "$name"(MavenPublication) {
                from components.java
                pom.withXml {
                    asNode().appendNode('name', name)
                    asNode().appendNode('description', description)
                    asNode().appendNode('url', 'http://ical4j.github.io')

                    def scmNode = asNode().appendNode('scm')
                    scmNode.appendNode('url', 'https://github.com/ical4j/ical4j-integration')
                    scmNode.appendNode('connection', 'scm:git@github.com:ical4j/ical4j-integration.git')
                    scmNode.appendNode('developerConnection', 'scm:git@github.com:ical4j/ical4j-integration.git')

                    def licenseNode = asNode().appendNode('licenses').appendNode('license')
                    licenseNode.appendNode('name', 'iCal4j - License')
                    licenseNode.appendNode('url', 'https://raw.githubusercontent.com/ical4j/ical4j/master/LICENSE')
                    licenseNode.appendNode('distribution', 'repo')

                    def developerNode = asNode().appendNode('developers').appendNode('developer')
                    developerNode.appendNode('id', 'fortuna')
                    developerNode.appendNode('name', 'Ben Fortuna')
                }
            }
        }

        repositories {
            maven {
                name = "OSSRH"
                url = version.endsWith('SNAPSHOT') ? "https://s01.oss.sonatype.org/content/repositories/snapshots/" : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }

    signing {
        required { isReleaseVersion }
        sign publishing.publications[name]
    }
}
